prefix foaf: <http://xmlns.com/foaf/0.1/>
prefix dc: <http://purl.org/dc/elements/1.1/>
prefix dcterms: <http://purl.org/dc/terms/>
prefix lv: <http://purl.org/lobid/lv#>

construct {
    ?resourceIri
        dc:type lv:ArchivedWebPage ;
        dc:identifier ?resourceIri, ?identifier ;
        dc:date ?datestamp ;
        dc:relation ?work, ?workIdentifier ;
        dc:format <https://d-nb.info/gnd/4596172-4> , ?medium ;
        dc:source ?url .
} where {
    ?resourceIri a lv:ArchivedWebPage ;
        dcterms:medium ?medium ;
        dc:identifier ?identifier ;
        dc:date ?datestamp ;
        foaf:primaryTopic ?url ;
        dcterms:isPartOf ?work .
    optional {
        ?work dc:identifier ?workIdentifier
    }
}