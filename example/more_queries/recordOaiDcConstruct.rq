prefix foaf: <http://xmlns.com/foaf/0.1/>
prefix bibo: <http://purl.org/ontology/bibo/>
prefix dc: <http://purl.org/dc/elements/1.1/>
prefix rdau: <http://rdaregistry.info/Elements/u/>
prefix dcterms: <http://purl.org/dc/terms/>
prefix lv:   <http://purl.org/lobid/lv#>
prefix bf: <http://id.loc.gov/ontologies/bibframe/>

construct {
    ?resourceIri
        dc:type <http://rdaregistry.info/termList/RDACarrierType/1018>, <http://rdaregistry.info/termList/RDAContentType/1020>, <http://rdaregistry.info/termList/RDAMediaType/1003> ;
        dc:identifier ?resourceIri, ?identifier ;
        dc:date ?datestamp ;
        dc:relation ?work, ?workIdentifier ;
        dc:subject ?url ; # could also be the link to the WARC file
        # dc:format "application/warc" ;
        dc:source ?url .
} where {
    ?resourceIri
        dcterms:medium <http://rdaregistry.info/termList/RDACarrierType/1018> ;
        rdau:P60048 <http://rdaregistry.info/termList/RDACarrierType/1018> ;
        rdau:P60049 <http://rdaregistry.info/termList/RDAContentType/1020> ;
        rdau:P60050 <http://rdaregistry.info/termList/RDAMediaType/1003> ;
        dc:identifier ?identifier ;
        bibo:issue ?datestamp ;
        dcterms:isPartOf ?work .
    ?work foaf:primaryTopic ?url .
    optional {
        ?work dc:identifier ?workIdentifier
    }
}